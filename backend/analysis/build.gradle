import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.2.21'
    id "org.jetbrains.kotlin.kapt" version '2.2.21'

    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.18.0'
}

group = 'ua.com.goit.clearbreath'
version = '0.0.1-SNAPSHOT'
description = 'Core service for clear breath'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //Spring WebFlux and R2DBC
    implementation platform('io.awspring.cloud:spring-cloud-aws:3.4.2')
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation "io.awspring.cloud:spring-cloud-aws-starter-sqs"
    implementation 'io.projectreactor.kotlin:reactor-kotlin-extensions'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.liquibase:liquibase-core'
    runtimeOnly "org.postgresql:r2dbc-postgresql"
    runtimeOnly 'org.postgresql:postgresql'
    implementation "org.mapstruct:mapstruct:1.5.5.Final"
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'
    implementation 'io.swagger.core.v3:swagger-annotations-jakarta:2.2.24'

    implementation("org.mindrot:jbcrypt:0.4")
    implementation("io.jsonwebtoken:jjwt-api:0.12.5")
    runtimeOnly("io.jsonwebtoken:jjwt-impl:0.12.5")
    runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.12.5")

    implementation(platform("software.amazon.awssdk:bom:2.25.60"))
    implementation("software.amazon.awssdk:s3")
    implementation("software.amazon.awssdk:sqs")

    annotationProcessor "org.mapstruct:mapstruct-processor:1.5.5.Final"
    kapt "org.mapstruct:mapstruct-processor:1.5.5.Final"
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict', '-Xannotation-default-target=param-property'
    }
}

def contractDir = findDirUpwards(layout.projectDirectory.asFile, "contract")
def repoRootDir = contractDir.parentFile
def openApiOut = layout.buildDirectory.dir("generated/openapi")

tasks.register('openApiGenerateCore', GenerateTask) {
    generatorName = "kotlin-spring"
    inputSpec = new File(repoRootDir, "contract/core-api.yaml").absolutePath
    outputDir = openApiOut.get().asFile.absolutePath + "/core"
    apiPackage = "ua.com.goit.clearbreath.analysis.api"
    modelPackage = "ua.com.goit.clearbreath.analysis.model"
    invokerPackage = "ua.com.goit.clearbreath.analysis.invoker"
    globalProperties = [apis: "", models: ""]
    configOptions = [
            interfaceOnly       : "true",
            useSpringBoot3      : "true",
            reactive            : "true",
            serializationLibrary: "jackson",
            dateLibrary         : "java8",
            enumPropertyNaming  : "UPPERCASE",
            useTags             : "true"
    ]
    generateApiTests = false
    generateModelTests = false
    generateApiDocumentation = false
    generateModelDocumentation = false

}

tasks.register('openApiGenerateUser', GenerateTask) {
    generatorName = "kotlin-spring"
    inputSpec = new File(repoRootDir, "contract/user-api.yaml").absolutePath
    outputDir = openApiOut.get().asFile.absolutePath + "/user"
    apiPackage = "ua.com.goit.clearbreath.analysis.api"
    modelPackage = "ua.com.goit.clearbreath.analysis.model"
    invokerPackage = "ua.com.goit.clearbreath.analysis.invoker"
    globalProperties = [apis: "", models: ""]
    configOptions = [
            interfaceOnly        : "true",
            useSpringBoot3       : "true",
            reactive             : "true",
            serializationLibrary : "jackson",
            dateLibrary          : "java8",
            enumPropertyNaming   : "UPPERCASE",
            useTags              : "true",
            documentationProvider: "none"
    ]
    generateApiTests = false
    generateModelTests = false
    generateApiDocumentation = false
    generateModelDocumentation = false

    schemaMappings = [
            "ErrorResponse": "ua.com.goit.clearbreath.model.ErrorResponse",
    ]
}

tasks.register('openApiGenerateAuth', GenerateTask) {
    generatorName = "kotlin-spring"
    inputSpec = new File(repoRootDir, "contract/auth-api.yaml").absolutePath
    outputDir = openApiOut.get().asFile.absolutePath + "/auth"
    apiPackage = "ua.com.goit.clearbreath.analysis.api"
    modelPackage = "ua.com.goit.clearbreath.analysis.model"
    invokerPackage = "ua.com.goit.clearbreath.analysis.invoker"
    globalProperties = [apis: "", models: ""]
    configOptions = [
            interfaceOnly        : "true",
            useSpringBoot3       : "true",
            reactive             : "true",
            serializationLibrary : "jackson",
            dateLibrary          : "java8",
            enumPropertyNaming   : "UPPERCASE",
            useTags              : "true",
            documentationProvider: "none"
    ]
    generateApiTests = false
    generateModelTests = false
    generateApiDocumentation = false
    generateModelDocumentation = false

    schemaMappings = [
            "ErrorResponse": "ua.com.goit.clearbreath.model.ErrorResponse",
    ]
}

def asyncapiSpec = new File(repoRootDir, "contract/events.yaml").absolutePath
def asyncApiOutDir = layout.buildDirectory.dir("generated/asyncapi/src/main/kotlin/ua.com.goit.clearbreath.analysis.events")

tasks.register("generateAsyncApiModels", Exec) {
    inputs.file(asyncapiSpec)
    outputs.dir(asyncApiOutDir)

    commandLine "npx",
            "@asyncapi/cli",
            "generate", "models", "kotlin",
            asyncapiSpec,
            "--packageName", "ua.com.goit.clearbreath.analysis.events",
            "-o", asyncApiOutDir.get().asFile.absolutePath
}

tasks.register("patchAsyncApiModels") {
    // This task adds @JsonValue annotation to enum classes generated by AsyncAPI generator
    dependsOn("generateAsyncApiModels")

    doLast {
        fileTree(asyncApiOutDir).matching { include "**/*.kt" }.files.each { f ->
            def txt = f.getText("UTF-8")

            if (txt.contains("enum class") && txt.contains("(val value: String)")) {

                if (!txt.contains("com.fasterxml.jackson.annotation.JsonValue")) {
                    txt = txt.replaceFirst(
                            /(?m)^package\s+[a-zA-Z0-9_.]+\s*$/,
                            { fullMatch -> fullMatch + "\n\nimport com.fasterxml.jackson.annotation.JsonValue" }
                    )
                }

                txt = txt.replace(
                        "(val value: String)",
                        "(@get:JsonValue val value: String)"
                )

                f.write(txt, "UTF-8")
            }
        }
    }
}

tasks.configureEach {
    if (name.contains("Kotlin") || name.startsWith("kapt")) {
        dependsOn("openApiGenerateCore", "openApiGenerateUser", "openApiGenerateAuth", "generateAsyncApiModels",
                "patchAsyncApiModels"
        )
    }
}

sourceSets.main.kotlin.srcDirs += [
        openApiOut.map { it.dir("core/src/main/kotlin") }.get().asFile,
        openApiOut.map { it.dir("user/src/main/kotlin") }.get().asFile,
        openApiOut.map { it.dir("auth/src/main/kotlin") }.get().asFile,
        asyncApiOutDir.get().asFile,
]

tasks.named('test') {
    useJUnitPlatform()
}

static def findDirUpwards(File startDir, String dirName) {
    File current = startDir

    while (current != null) {
        File candidate = new File(current, dirName)
        if (candidate.exists() && candidate.isDirectory()) {
            return candidate
        }
        current = current.parentFile
    }

    throw new GradleException(
            "Directory '${dirName}' not found starting from ${startDir.absolutePath}"
    )
}


