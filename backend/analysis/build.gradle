import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.2.21'
    id "org.jetbrains.kotlin.kapt" version '2.2.21'

    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.18.0'
}

group = 'ua.com.goit.clearbreath'
version = '0.0.1-SNAPSHOT'
description = 'Core service for clear breath'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //Spring WebFlux and R2DBC
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'io.projectreactor.kotlin:reactor-kotlin-extensions'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.liquibase:liquibase-core'
    runtimeOnly "org.postgresql:r2dbc-postgresql"
    runtimeOnly 'org.postgresql:postgresql'
    implementation "org.mapstruct:mapstruct:1.5.5.Final"
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'
    //OpenAPI and Swagger
    implementation 'io.swagger.core.v3:swagger-annotations-jakarta:2.2.24'

    implementation("org.mindrot:jbcrypt:0.4")
    // JWT
    implementation("io.jsonwebtoken:jjwt-api:0.12.5")
    runtimeOnly("io.jsonwebtoken:jjwt-impl:0.12.5")
    runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.12.5")

    //AWS
    implementation(platform("software.amazon.awssdk:bom:2.25.60"))
    implementation("software.amazon.awssdk:s3")
    implementation("software.amazon.awssdk:sqs")

    annotationProcessor "org.mapstruct:mapstruct-processor:1.5.5.Final"
    kapt "org.mapstruct:mapstruct-processor:1.5.5.Final"
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict', '-Xannotation-default-target=param-property'
    }
}

def repoRootDir = layout.projectDirectory.dir("../..")
def openApiOut = layout.buildDirectory.dir("generated/openapi")

tasks.register('openApiGenerateCore', GenerateTask) {
    generatorName = "kotlin-spring"
    inputSpec = repoRootDir.file("contract/core-api.yaml").asFile.absolutePath
    outputDir = openApiOut.get().asFile.absolutePath + "/core"
    apiPackage = "ua.com.goit.clearbreath.analysis.api"
    modelPackage = "ua.com.goit.clearbreath.analysis.model"
    invokerPackage = "ua.com.goit.clearbreath.analysis.invoker"
    globalProperties = [apis: "", models: ""]
    configOptions = [
            interfaceOnly: "true",
            useSpringBoot3: "true",
            reactive: "true",
            serializationLibrary: "jackson",
            dateLibrary: "java8",
            enumPropertyNaming: "UPPERCASE",
            useTags: "true"
    ]
    generateApiTests = false
    generateModelTests = false
    generateApiDocumentation = false
    generateModelDocumentation = false

}

tasks.register('openApiGenerateUser', GenerateTask) {
    generatorName = "kotlin-spring"
    inputSpec = repoRootDir.file("contract/user-api.yaml").asFile.absolutePath
    outputDir = openApiOut.get().asFile.absolutePath + "/user"
    apiPackage = "ua.com.goit.clearbreath.analysis.api"
    modelPackage = "ua.com.goit.clearbreath.analysis.model"
    invokerPackage = "ua.com.goit.clearbreath.analysis.invoker"
    globalProperties = [apis: "", models: ""]
    configOptions = [
            interfaceOnly: "true",
            useSpringBoot3: "true",
            reactive: "true",
            serializationLibrary: "jackson",
            dateLibrary: "java8",
            enumPropertyNaming: "UPPERCASE",
            useTags: "true",
            documentationProvider: "none"
    ]
    generateApiTests = false
    generateModelTests = false
    generateApiDocumentation = false
    generateModelDocumentation = false

    schemaMappings = [
            "ErrorResponse": "ua.com.goit.clearbreath.model.ErrorResponse",
    ]
}

tasks.register('openApiGenerateAuth', GenerateTask) {
    generatorName = "kotlin-spring"
    inputSpec = repoRootDir.file("contract/auth-api.yaml").asFile.absolutePath
    outputDir = openApiOut.get().asFile.absolutePath + "/auth"
    apiPackage = "ua.com.goit.clearbreath.analysis.api"
    modelPackage = "ua.com.goit.clearbreath.analysis.model"
    invokerPackage = "ua.com.goit.clearbreath.analysis.invoker"
    globalProperties = [apis: "", models: ""]
    configOptions = [
            interfaceOnly: "true",
            useSpringBoot3: "true",
            reactive: "true",
            serializationLibrary: "jackson",
            dateLibrary: "java8",
            enumPropertyNaming: "UPPERCASE",
            useTags: "true",
            documentationProvider: "none"
    ]
    generateApiTests = false
    generateModelTests = false
    generateApiDocumentation = false
    generateModelDocumentation = false

    schemaMappings = [
            "ErrorResponse": "ua.com.goit.clearbreath.model.ErrorResponse",
    ]
}

tasks.configureEach {
    if (name.contains("Kotlin") || name.startsWith("kapt")) {
        dependsOn("openApiGenerateCore" , "openApiGenerateUser", "openApiGenerateAuth")
    }
}

sourceSets.main.kotlin.srcDirs += [
        openApiOut.map { it.dir("core/src/main/kotlin") }.get().asFile,
        openApiOut.map { it.dir("user/src/main/kotlin") }.get().asFile,
        openApiOut.map { it.dir("auth/src/main/kotlin") }.get().asFile
]

tasks.named('test') {
    useJUnitPlatform()
}


